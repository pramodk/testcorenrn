{load_file("nrngui.hoc")}

begintemplate Cell
public soma, syn, stim
create soma
objref syn, stim, ramps_t, ramps_i, r
proc init() {local tt, x
  r = new Random()
  r.Random123($1, 0, 0)
  ramps_t = new Vector()
  ramps_i = new Vector()
  for (tt=0; tt < 100; tt = tt + 5) {
    x = r.uniform(.5, 1.5)
    ramps_t.append(tt, tt+x, tt+x, tt+5)
    ramps_i.append(0, .01+.001*($1+1), 0, -.001)
  }
  soma {
    nseg=1
    diam = 1  L = 1
    insert hh
    stim = new IClamp(.5)
    stim.amp= 0
    stim.dur = 1e9
    ramps_i.play(stim, &stim.amp, ramps_t, 1)
  }
}

endtemplate Cell

ncell = 10
objref pc, cell, cells, nc, netcons, st, stims, nil
pc = new ParallelContext()

cells = new List()

for (gid = pc.id; gid < ncell; gid += pc.nhost) {
  pc.set_gid2node(gid, pc.id)
  cell = new Cell(gid, ncell)
  cell.soma pc.cell(gid, new NetCon(&cell.soma.v(.5), nil))
//  cell.syn.noiseFromRandom123(gid,2,3)
  cells.append(cell)
}

objref tvec, idvec
tvec = new Vector()
idvec = new Vector()
pc.spike_record(-1, tvec, idvec)

cvode.cache_efficient(1)
pc.set_maxstep(10)
stdinit()

proc pcell() {localobj s
  s = new String()
  sprint(s.s, "t%g", t)
  pc.prcellstate($1, s.s)
}

pc.nrnbbcore_write("testvecplaydat")
//pcell(0)
tsav = startsw()
pc.psolve(100)
if (pc.id == 0) { printf("psolve time %g\n", startsw() - tsav) }
//pcell(0)

strdef s
sprint(s, "out%d.dat", pc.id)
objref f
f = new File()
f.wopen(s)
for i=0, tvec.size-1 {
  f.printf("%g %d\n", tvec.x[i], idvec.x[i])
}
f.close()

pc.barrier()
quit()

